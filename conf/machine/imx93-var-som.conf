#@TYPE: Machine
#@NAME: Variscite VAR-SOM-MX93
#@SOC: i.MX93
#@DESCRIPTION: Machine configuration for Variscite VAR-SOM-MX93
#@MAINTAINER: Nate Drude <nate.d@variscite.com>
#
# http://www.variscite.com
MACHINEOVERRIDES =. "mx93:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc

require variscite.inc
require variscite_bcm43xx.inc

# Variscite BSP default providers
PREFERRED_PROVIDER_virtual/kernel:imx93-var-som ?= "linux-variscite"
PREFERRED_PROVIDER_u-boot:imx93-var-som ?= "u-boot-variscite"
PREFERRED_PROVIDER_u-boot-fw-utils:imx93-var-som ?= "u-boot-fw-utils"
PREFERRED_PROVIDER_virtual/bootloader:imx93-var-som ?= "u-boot-variscite"
PREFERRED_RPROVIDER_ethos-u-firmware ?= "freertos-variscite"

MACHINE_FEATURES += "pci wifi bluetooth iw612"

KERNEL_DEVICETREE = " \
    freescale/imx93-var-som-symphony.dtb \
    freescale/imx93-var-som-symphony-m33.dtb \
    \
    freescale/imx93-var-som-1.x-symphony.dtb \
    freescale/imx93-var-som-1.x-symphony-m33.dtb \
"
UBOOT_DTB_NAME = "imx93-var-som-symphony.dtb"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""

SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "imx93_var_som_defconfig,sdcard"

# Set ATF platform name
ATF_PLATFORM = "imx93"

IMXBOOT_TARGETS_SD   = "flash_singleboot"
IMXBOOT_TARGETS = "${IMXBOOT_TARGETS_SD}"

IMX_BOOT_SOC_TARGET = "iMX9"
IMX_BOOT_SEEK = "32"

# We have to disable SERIAL_CONSOLE due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

# Add additional firmware
MACHINE_FIRMWARE:append = " ethos-u-firmware"

DDR_FIRMWARE_NAME = " \
        lpddr4_dmem_1d_v202201.bin \
        lpddr4_dmem_2d_v202201.bin \
        lpddr4_imem_1d_v202201.bin \
        lpddr4_imem_2d_v202201.bin \
"

SECO_FIRMWARE_NAME:mx9-nxp-bsp = "mx93a0-ahab-container.img"

MACHINE_EXTRA_RRECOMMENDS:remove = "imx-m33-demos"